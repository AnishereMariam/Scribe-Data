# tool: scribe-data
# All Danish (Q9035) adjectives and some of the available forms.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?singular
  ?plural
  ?common
  ?neuter

WHERE {
  ?lexeme dct:language wd:Q9035 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

    # MARK: Singular
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786.
  } .

  # MARK: Plural
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .

 # MARK: Common
 OPTIONAL {
    ?lexeme ontolex:lexicalForm ?commonForm .
    ?commonForm ontolex:representation ?common;
      wikibase:grammaticalFeature wd:Q1305037 .
  } .

  # MARK: Neuter
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterForm .
    ?neuterForm ontolex:representation ?neuter ;
      wikibase:grammaticalFeature wd:Q1775461 .
  } .
}
