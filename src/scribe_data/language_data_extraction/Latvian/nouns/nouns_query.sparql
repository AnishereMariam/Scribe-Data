# tool: scribe-data
# All Latvian (Q9078) nouns, their plurals and their genders.
# Enter this query at https://query.wikidata.org/.
# All Masculine and Feminine forms of nouns for latvian 

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?singular
  ?plural
  ?gender

WHERE {
  VALUES ?nounTypes {wd:Q1084 wd:Q147276} # Nouns & pronouns

  ?lexeme dct:language wd:Q9078 ;
    wikibase:lexicalCategory ?nounTypes ;
    wikibase:lemma ?singular .
 
  # MARK: Plural
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .

  # MARK: Gender(s)
  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
    FILTER NOT EXISTS {
      ?lexeme wdt:P31 wd:Q202444 .
    }
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
