# tool: scribe-data
# All Arabic (Q13955) adjectives and their grammatical features.
# Enter this query at https://query.wikidata.org/.

SELECT ?lexemeId ?Adjective (GROUP_CONCAT(?featureLabel; separator=", ") AS ?features) WHERE {
  # MARK: Filter adjectives
  ?lexeme dct:language wd:Q13955;
          wikibase:lexicalCategory wd:Q34698.

  # MARK: Retrieve forms
  ?lexeme ontolex:lexicalForm ?form.

  BIND(STRAFTER(STR(?form), "entity/") AS ?lexemeId).

  # MARK: Get word
  ?form ontolex:representation ?Adjective.

  # MARK: Get features
  OPTIONAL { ?form wikibase:grammaticalFeature ?feature. }

  # MARK: Labeling
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
    ?feature rdfs:label ?featureLabel.
  }
}
# MARK: Grouping
GROUP BY ?lexemeId ?Adjective
# MARK: Order results
ORDER BY ?lexemeId
