# tool: scribe-data
# All Slovak (Q9058)adjectives in the given cases.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?nomSingular
  ?nomPlural
  ?genSingular
  ?genPlural
  ?datSingular
  ?datPlural

WHERE {
  ?lexeme dct:language wd:Q9058;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

  # MARK: Nominative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nomSingularForm .
    ?nomSingularForm ontolex:representation ?nomSingular ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q110786 .
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nomPluralForm .
    ?nomPluralForm ontolex:representation ?nomPlural ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .

  # MARK: Genitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genSingularForm .
    ?genSingularForm ontolex:representation ?genSingular ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q110786 .
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genPluralForm .
    ?genPluralForm ontolex:representation ?genPlural ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .

  # MARK: dative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?datSingularForm .
    ?datSingularForm ontolex:representation ?datSingular ;
      wikibase:grammaticalFeature wd:Q145599 ;
      wikibase:grammaticalFeature wd:Q110786 .
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?datPluralForm .
    ?datPluralForm ontolex:representation ?datPlural ;
      wikibase:grammaticalFeature wd:Q145599 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .
}
