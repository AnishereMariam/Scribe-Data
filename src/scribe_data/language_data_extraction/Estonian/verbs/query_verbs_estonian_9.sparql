# tool: scribe-data
# Enter this query at https://query.wikidata.org/
# Retrieve Estonian Verbs with First Person and Second Person forms 547540 results in 19061 ms

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lemma
  ?firstPerson
  ?secondPerson
WHERE {
  ?lexeme dct:language wd:Q9072 ;  # Estonian language
          wikibase:lexicalCategory wd:Q24905 ;  # Verb category
          wikibase:lemma ?lemma .
  FILTER(lang(?lemma) = "et")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonForm .
    ?firstPersonForm ontolex:representation ?firstPerson ;
    wikibase:grammaticalFeature wd:Q21714344 .  # First person
    FILTER(LANG(?firstPerson) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonForm .
    ?secondPersonForm ontolex:representation ?secondPerson ;
    wikibase:grammaticalFeature wd:Q51929049 .  # Second person
    FILTER(LANG(?secondPerson) = "et")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
  }
}
