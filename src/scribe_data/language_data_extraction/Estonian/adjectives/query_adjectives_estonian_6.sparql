# tool: scribe-data
# Enter this query at https://query.wikidata.org/
# Retrieve Estonian Adjectives with Plural Cases 193387 results in 4408 ms

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lemma
  ?plural
WHERE {
  ?lexeme dct:language wd:Q9072 ;
          wikibase:lexicalCategory wd:Q34698 ;
          wikibase:lemma ?lemma .
  FILTER(lang(?lemma) = "et")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
    wikibase:grammaticalFeature wd:Q146786 .
    FILTER(LANG(?plural) = "et")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
  }
}
