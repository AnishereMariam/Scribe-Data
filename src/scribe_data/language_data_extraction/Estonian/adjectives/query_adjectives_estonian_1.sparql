# tool: scribe-data
# Enter this query at https://query.wikidata.org/
# Retrieve Estonian Adjectives with Nominative, Partitive, and Genitive Cases 84663 results in 21031 ms

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lemma
  ?nominative
  ?partitive
  ?genitive
WHERE {
  ?lexeme dct:language wd:Q9072 ;
          wikibase:lexicalCategory wd:Q34698 ;
          wikibase:lemma ?lemma .
  FILTER(lang(?lemma) = "et")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
    wikibase:grammaticalFeature wd:Q131105 .
    FILTER(LANG(?nominative) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?partitiveForm .
    ?partitiveForm ontolex:representation ?partitive ;
    wikibase:grammaticalFeature wd:Q857325 .
    FILTER(LANG(?partitive) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
    wikibase:grammaticalFeature wd:Q146233 .
    FILTER(LANG(?genitive) = "et")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
  }
}
