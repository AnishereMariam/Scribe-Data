# tool: scribe-data
# Enter this query at https://query.wikidata.org/
# Retrieve Estonian Adjectives with Comitative, Abessive, and Translative Cases 122983 results in 15312 ms

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lemma
  ?comitative
  ?abessive
  ?translative
WHERE {
  ?lexeme dct:language wd:Q9072 ;
          wikibase:lexicalCategory wd:Q34698 ;
          wikibase:lemma ?lemma .
  FILTER(lang(?lemma) = "et")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comitativeForm .
    ?comitativeForm ontolex:representation ?comitative ;
    wikibase:grammaticalFeature wd:Q838581 .
    FILTER(LANG(?comitative) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?abessiveForm .
    ?abessiveForm ontolex:representation ?abessive ;
    wikibase:grammaticalFeature wd:Q319822 .
    FILTER(LANG(?abessive) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?translativeForm .
    ?translativeForm ontolex:representation ?translative ;
    wikibase:grammaticalFeature wd:Q950170 .
    FILTER(LANG(?translative) = "et")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
  }
}
