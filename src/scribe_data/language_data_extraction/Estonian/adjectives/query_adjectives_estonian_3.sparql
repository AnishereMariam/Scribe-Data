# tool: scribe-data
# Enter this query at https://query.wikidata.org/
# Retrieve Estonian Adjectives with Adessive, Allative, and Ablative Cases 254332 results in 2870 ms

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lemma
  ?adessive
  ?allative
  ?ablative
WHERE {
  ?lexeme dct:language wd:Q9072 ;
          wikibase:lexicalCategory wd:Q34698 ;
          wikibase:lemma ?lemma .
  FILTER(lang(?lemma) = "et")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?adessiveForm .
    ?adessiveForm ontolex:representation ?adessive ;
    wikibase:grammaticalFeature wd:Q281954 .
    FILTER(LANG(?adessive) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?allativeForm .
    ?allativeForm ontolex:representation ?allative ;
    wikibase:grammaticalFeature wd:Q655020 .
    FILTER(LANG(?allative) = "et")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?ablativeForm .
    ?ablativeForm ontolex:representation ?ablative ;
    wikibase:grammaticalFeature wd:Q156986 .
    FILTER(LANG(?ablative) = "et")
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
  }
}
